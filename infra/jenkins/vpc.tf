resource "aws_vpc" "main" {
    cidr_block           = var.vpc_cidr
    enable_dns_support   = true
    enable_dns_hostnames = true
    tags                 = merge(local.common_tags, { Name = "jenkins-vpc" })
}

resource "aws_subnet" "public" {
    vpc_id            = aws_vpc.main.id
    cidr_block        = var.public_subnet_cidr
    availability_zone = var.availability_zone
    tags              = merge(local.common_tags, { Name = "jenkins-public-subnet" })
}

resource "aws_internet_gateway" "main" {
    vpc_id = aws_vpc.main.id
    tags   = merge(local.common_tags, { Name = "jenkins-igw" })
}

resource "aws_route_table" "public" {
    vpc_id = aws_vpc.main.id
    route {
        cidr_block = "0.0.0.0/0"
        gateway_id = aws_internet_gateway.main.id
    }
    tags = merge(local.common_tags, { Name = "jenkins-public-rt" })
}

resource "aws_route_table_association" "public" {
    subnet_id = aws_subnet.public.id
    route_table_id = aws_route_table.public.id
}
